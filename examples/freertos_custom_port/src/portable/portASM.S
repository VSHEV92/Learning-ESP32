
#include "portContext.h"

#define TIMER_INTERRUPT_ID 1
.global clear_timer_interrupt

.global xPortStartFirstTask
.global pxPortInitialiseStack
.global freertos_risc_v_trap_handler
.global freertos_risc_v_exception_handler
.global freertos_risc_v_interrupt_handler

.extern vTaskSwitchContext
.extern xTaskIncrementTick
.extern uxTimerIncrementsForOneTick /* size_t type so 32-bit on 32-bit core and 64-bits on 64-bit core. */
.extern xTaskReturnAddress

.weak freertos_risc_v_application_exception_handler
.weak freertos_risc_v_application_interrupt_handler
/*-----------------------------------------------------------*/

/*
 * Unlike other ports pxPortInitialiseStack() is written in assembly code as it
 * needs access to the portasmADDITIONAL_CONTEXT_SIZE constant.  The prototype
 * for the function is as per the other ports:
 * StackType_t *pxPortInitialiseStack( StackType_t *pxTopOfStack, TaskFunction_t pxCode, void *pvParameters );
 *
 * As per the standard RISC-V ABI pxTopOfStack is passed in in a0, pxCode in
 * a1, and pvParameters in a2.  The new top of stack is passed out in a0.
 *
 * RISC-V maps registers to ABI names as follows (X1 to X31 integer registers
 * for the 'I' profile, X1 to X15 for the 'E' profile, currently I assumed).
 *
 * Register      ABI Name    Description                       Saver
 * x0            zero        Hard-wired zero                   -
 * x1            ra          Return address                    Caller
 * x2            sp          Stack pointer                     Callee
 * x3            gp          Global pointer                    -
 * x4            tp          Thread pointer                    -
 * x5-7          t0-2        Temporaries                       Caller
 * x8            s0/fp       Saved register/Frame pointer      Callee
 * x9            s1          Saved register                    Callee
 * x10-11        a0-1        Function Arguments/return values  Caller
 * x12-17        a2-7        Function arguments                Caller
 * x18-27        s2-11       Saved registers                   Callee
 * x28-31        t3-6        Temporaries                       Caller
 *
 * The RISC-V context is saved to FreeRTOS tasks in the following stack frame,
 * where the global and thread pointers are currently assumed to be constant so
 * are not saved:
 *
 * xCriticalNesting
 * x31
 * x30
 * x29
 * x28
 * x27
 * x26
 * x25
 * x24
 * x23
 * x22
 * x21
 * x20
 * x19
 * x18
 * x17
 * x16
 * x15
 * x14
 * x13
 * x12
 * x11
 * pvParameters
 * x9
 * x8
 * x7
 * x6
 * x5
 * portTASK_RETURN_ADDRESS
 * [FPU registers (when enabled/available) go here]
 * [VPU registers (when enabled/available) go here]
 * [chip specific registers go here]
 * mstatus
 * pxCode
 */
pxPortInitialiseStack:
    addi a0, a0, -portWORD_SIZE         /* Space for critical nesting count. */
    store_x x0, 0(a0)                   /* Critical nesting count starts at 0 for every task. */

    addi a0, a0, -(22 * portWORD_SIZE)  /* Space for registers x10-x31. */

    store_x a2, 0(a0)                   /* Task parameters (pvParameters parameter) goes into register x10/a0 on the stack. */

    addi a0, a0, -(6 * portWORD_SIZE)   /* Space for registers x5-x9 + taskReturnAddress (register x1). */
    load_x t0, xTaskReturnAddress
    store_x t0, 0(a0)                   /* Return address onto the stack. */

    addi t0, x0, portasmADDITIONAL_CONTEXT_SIZE /* The number of chip specific additional registers. */
chip_specific_stack_frame:              /* First add any chip specific registers to the stack frame being created. */
    beq t0, x0, 1f                      /* No more chip specific registers to save. */
    addi a0, a0, -portWORD_SIZE         /* Make space for chip specific register. */
    store_x x0, 0(a0)                   /* Give the chip specific register an initial value of zero. */
    addi t0, t0, -1                     /* Decrement the count of chip specific registers remaining. */
    j chip_specific_stack_frame         /* Until no more chip specific registers. */
1:
    csrr t0, mstatus                    /* Obtain current mstatus value. */
    andi t0, t0, ~0x8                   /* Ensure interrupts are disabled when the stack is restored within an ISR.  Required when a task is created after the scheduler has been started, otherwise interrupts would be disabled anyway. */
    addi t1, x0, 0x188                  /* Generate the value 0x1880, which are the MPIE=1 and MPP=M_Mode in mstatus. */
    slli t1, t1, 4
    or t0, t0, t1                       /* Set MPIE and MPP bits in mstatus value. */

    addi a0, a0, -portWORD_SIZE
    store_x t0, 0(a0)                   /* mstatus onto the stack. */

    addi a0, a0, -portWORD_SIZE
    store_x a1, 0(a0)                   /* mret value (pxCode parameter) onto the stack. */
    ret
/*-----------------------------------------------------------*/

xPortStartFirstTask:
    load_x  sp, pxCurrentTCB            /* Load pxCurrentTCB. */
    load_x  sp, 0( sp )                 /* Read sp from first TCB member. */

    load_x  x1, 0( sp ) /* Note for starting the scheduler the exception return address is used as the function return address. */

    load_x  x5, 1 * portWORD_SIZE( sp ) /* Initial mstatus into x5 (t0). */
    addi    x5, x5, 0x08                /* Set MIE bit so the first task starts with interrupts enabled - required as returns with ret not eret. */
    csrw    mstatus, x5                 /* Interrupts enabled from here! */

    load_x  x7,  5  * portWORD_SIZE( sp )   /* t2 */
    load_x  x8,  6  * portWORD_SIZE( sp )   /* s0/fp */
    load_x  x9,  7  * portWORD_SIZE( sp )   /* s1 */
    load_x  x10, 8  * portWORD_SIZE( sp )   /* a0 */
    load_x  x11, 9  * portWORD_SIZE( sp )   /* a1 */
    load_x  x12, 10 * portWORD_SIZE( sp )   /* a2 */
    load_x  x13, 11 * portWORD_SIZE( sp )   /* a3 */
    load_x  x14, 12 * portWORD_SIZE( sp )   /* a4 */
    load_x  x15, 13 * portWORD_SIZE( sp )   /* a5 */
    load_x  x16, 14 * portWORD_SIZE( sp )   /* a6 */
    load_x  x17, 15 * portWORD_SIZE( sp )   /* a7 */
    load_x  x18, 16 * portWORD_SIZE( sp )   /* s2 */
    load_x  x19, 17 * portWORD_SIZE( sp )   /* s3 */
    load_x  x20, 18 * portWORD_SIZE( sp )   /* s4 */
    load_x  x21, 19 * portWORD_SIZE( sp )   /* s5 */
    load_x  x22, 20 * portWORD_SIZE( sp )   /* s6 */
    load_x  x23, 21 * portWORD_SIZE( sp )   /* s7 */
    load_x  x24, 22 * portWORD_SIZE( sp )   /* s8 */
    load_x  x25, 23 * portWORD_SIZE( sp )   /* s9 */
    load_x  x26, 24 * portWORD_SIZE( sp )   /* s10 */
    load_x  x27, 25 * portWORD_SIZE( sp )   /* s11 */
    load_x  x28, 26 * portWORD_SIZE( sp )   /* t3 */
    load_x  x29, 27 * portWORD_SIZE( sp )   /* t4 */
    load_x  x30, 28 * portWORD_SIZE( sp )   /* t5 */
    load_x  x31, 29 * portWORD_SIZE( sp )   /* t6 */

    load_x  x5, portCRITICAL_NESTING_OFFSET * portWORD_SIZE( sp )    /* Obtain xCriticalNesting value for this task from task's stack. */
    load_x  x6, pxCriticalNesting           /* Load the address of xCriticalNesting into x6. */
    store_x x5, 0( x6 )                     /* Restore the critical nesting value for this task. */

    load_x  x5, 3 * portWORD_SIZE( sp )     /* Initial x5 (t0) value. */
    load_x  x6, 4 * portWORD_SIZE( sp )     /* Initial x6 (t1) value. */

    addi    sp, sp, portCONTEXT_SIZE
    ret
/*-----------------------------------------------------------*/

freertos_risc_v_application_exception_handler:
    csrr t0, mcause     /* For viewing in the debugger only. */
    csrr t1, mepc       /* For viewing in the debugger only */
    csrr t2, mstatus    /* For viewing in the debugger only */
    j .
/*-----------------------------------------------------------*/

freertos_risc_v_application_interrupt_handler:
    csrr t0, mcause     /* For viewing in the debugger only. */
    csrr t1, mepc       /* For viewing in the debugger only */
    csrr t2, mstatus    /* For viewing in the debugger only */
    j .
/*-----------------------------------------------------------*/

.section .text.freertos_risc_v_exception_handler
freertos_risc_v_exception_handler:
    portcontextSAVE_EXCEPTION_CONTEXT
    /* a0 now contains mcause. */
    li t0, 11                           /* 11 == environment call. */
    bne a0, t0, other_exception         /* Not an M environment call, so some other exception. */
    call vTaskSwitchContext
    portcontextRESTORE_CONTEXT

other_exception:
    call freertos_risc_v_application_exception_handler
    portcontextRESTORE_CONTEXT
/*-----------------------------------------------------------*/

.section .text.freertos_risc_v_interrupt_handler
freertos_risc_v_interrupt_handler:
    portcontextSAVE_INTERRUPT_CONTEXT
    call freertos_risc_v_application_interrupt_handler
    portcontextRESTORE_CONTEXT
/*-----------------------------------------------------------*/

.section .text.freertos_risc_v_trap_handler
.align 8
freertos_risc_v_trap_handler:
    portcontextSAVE_CONTEXT_INTERNAL

    csrr a0, mcause
    csrr a1, mepc

    bge a0, x0, synchronous_exception

asynchronous_interrupt:
    store_x a1, 0( sp )                 /* Asynchronous interrupt so save unmodified exception return address. */
    load_x sp, xISRStackTop             /* Switch to ISR stack. */
    j handle_interrupt

    j processed_source                  /* No other exceptions handled yet. */

synchronous_exception:
    addi a1, a1, 4                      /* Synchronous so update exception return address to the instruction after the instruction that generated the exeption. */
    store_x a1, 0( sp )                 /* Save updated exception return address. */
    load_x sp, xISRStackTop             /* Switch to ISR stack. */
    j handle_exception

handle_interrupt:
    test_if_timer_interrupt:            
        addi t0, x0, 1
        slli t0, t0, __riscv_xlen - 1   /* LSB is already set, shift into MSB.  Shift 31 on 32-bit or 63 on 64-bit cores. */
        addi t1, t0, TIMER_INTERRUPT_ID 
        bne a0, t1, application_interrupt_handler

        call clear_timer_interrupt
        call xTaskIncrementTick
        beqz a0, processed_source       /* Don't switch context if incrementing tick didn't unblock a task. */
        call vTaskSwitchContext
        j processed_source

application_interrupt_handler:
    call freertos_risc_v_application_interrupt_handler
    j processed_source

handle_exception:
    /* a0 contains mcause. */
    li t0, 11                                   /* 11 == environment call. */
    bne a0, t0, application_exception_handler   /* Not an M environment call, so some other exception. */
    call vTaskSwitchContext
    j processed_source

application_exception_handler:
    call freertos_risc_v_application_exception_handler
    j processed_source                  /* No other exceptions handled yet. */

processed_source:
    portcontextRESTORE_CONTEXT
/*-----------------------------------------------------------*/

/* vector table */
.balign 0x100

.global _custom_vector_table
.type _custom_vector_table, @function
_custom_vector_table:
    j freertos_risc_v_trap_handler        /* 0 */
    j freertos_risc_v_trap_handler        /* 1 */
    j freertos_risc_v_trap_handler        /* 2 */
    j freertos_risc_v_trap_handler        /* 3 */
    j freertos_risc_v_trap_handler        /* 4 */
    j freertos_risc_v_trap_handler        /* 5 */
    j freertos_risc_v_trap_handler        /* 6 */
    j freertos_risc_v_trap_handler        /* 7 */
    j freertos_risc_v_trap_handler        /* 8 */
    j freertos_risc_v_trap_handler        /* 9 */
    j freertos_risc_v_trap_handler        /* 10 */
    j freertos_risc_v_trap_handler        /* 11 */
    j freertos_risc_v_trap_handler        /* 12 */
    j freertos_risc_v_trap_handler        /* 13 */
    j freertos_risc_v_trap_handler        /* 14 */
    j freertos_risc_v_trap_handler        /* 15 */
    j freertos_risc_v_trap_handler        /* 16 */
    j freertos_risc_v_trap_handler        /* 17 */
    j freertos_risc_v_trap_handler        /* 18 */
    j freertos_risc_v_trap_handler        /* 19 */
    j freertos_risc_v_trap_handler        /* 20 */
    j freertos_risc_v_trap_handler        /* 21 */
    j freertos_risc_v_trap_handler        /* 22 */
    j freertos_risc_v_trap_handler        /* 23 */
    j freertos_risc_v_trap_handler        /* 24 */
    j freertos_risc_v_trap_handler        /* 25 */
    j freertos_risc_v_trap_handler        /* 26 */
    j freertos_risc_v_trap_handler        /* 27 */
    j freertos_risc_v_trap_handler        /* 28 */
    j freertos_risc_v_trap_handler        /* 29 */
    j freertos_risc_v_trap_handler        /* 30 */
    j freertos_risc_v_trap_handler        /* 31 */

.size _custom_vector_table, .-_custom_vector_table
